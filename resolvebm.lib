////////////////////////////////////////////////////////////////
info="
LIBRARY:   resolvebm.lib
AUTHOR:    Dominic Austria, email: dgaustri@uwaterloo.ca

";
////////////////////////////////////////////////////////////////

static proc mod_init()
{
	LIB "cideal.lib";
	LIB "hypersurface.lib";
	LIB "resolve.lib";

	//invData will be the data we need to do the resolution of singularities algorithm
	//after computing the invariant.
	newstruct("invData", "list invariant, list hypersurfaces");

	system("install", "invData", "string", string_inv_data, 1);
	system("install", "invData", "print", pretty_print_inv_data, 1);
	system("install", "invData", "=", make_inv_data, 1);

	newstruct("nodeData", "string idealStr, int depth, invData invariant");
	system("install", "nodeData", "=", make_node_data, 1);
	system("install", "nodeData", "print", print_node_data, 1);

	newstruct("blowUpData", "string ringvars, string ambientSpace, string strictTransform, string exceptionalDivisor, string blowUpMap");
	system("install", "blowUpData", "=", make_blowup_data, 1);
	system("install", "blowUpData", "print", print_blowup_data, 1);

	newstruct("tree_node", "nodeData params, blowUpData blowup");
	system("install", "tree_node", "=", make_tree_node, 1);
	system("install", "tree_node", "print", print_tree_node, 1);
}

proc string_inv_data(invData data)
{
	string out = "(invariant:"+string(data.invariant)+" hypersurfaces of max contact:"+string(data.hypersurfaces)+")";
	return(out);
}

proc pretty_print_inv_data(invData data)
{
	print(string(data));
}

proc update_invariant(invData data, poly hypersurface_max_cont, list new_data)
{
	if(hypersurface_max_cont != poly(0))
	{
		data.hypersurfaces = insert(data.hypersurfaces, hypersurface_max_cont);
	}

	data.invariant = data.invariant + new_data;
	return(data);
}

proc is_null(invData data)
{
	return(size(data.invariant) == 0);
}

proc make_inv_data(list invariant, list hypersurfaces)
{
	invData data;
	data.invariant = invariant;
	data.hypersurfaces = hypersurfaces;
	return(data);
}

proc make_null_inv_data()
{
	invData null_data = list(), list();
	return(null_data);
}

// returns 1 if left < right, false otherwise
proc inv_datacmp_lt(invData left, invData right)
{
	list left_inv = left.invariant;
	list right_inv = right.invariant;

	int min_size = min(size(left_inv), size(right_inv));

	for(int i = 1; i <= min_size; i++)
	{
		if(left_inv[i] < right_inv[i])
		{
			return(1);
		}
	}

	if(size(left_inv) < size(right_inv))
	{
		return(1);
	}

	return(0);
}

proc make_node_data(string idealStr, int depth, invData invariant)
{
	nodeData data;
	data.idealStr = idealStr;
	data.depth = depth;
	data.invariant = invariant;
	return(data);
}

proc make_blowup_data(string ringvars, string ambientSpace, string strictTransform, 
		string exceptionalDivisor, string blowUpMap)
{
	blowUpData data;
	data.ringvars = ringvars;
	data.ambientSpace = ambientSpace;
	data.strictTransform = strictTransform;
	data.exceptionalDivisor = exceptionalDivisor;
	data.blowUpMap = blowUpMap;
	return(data);
}

proc make_tree_node(nodeData params, blowUpData blowup)
{
	tree_node node;
	node.params = params;
	node.blowup = blowup;
	return(node);
}

proc print_node_data(nodeData node)
{
        print("=================PARAMETERS===============");
        print("");
        print("ideal: " + node.idealStr);
        print("");
        print("depth: " + string(node.depth));
        print("");
        print("invariant: " + string(node.invariant.invariant));
}

proc print_blowup_data(blowUpData blowup)
{
        print("==================BLOWUP==================");
        print("");
        print("ring variables: " + varstr(basering));
        print("");
        print("ideal of blownup ambient space:"+string(blowup.ambientSpace));
        print("");
        print("ideal of strict transform:"+string(blowup.strictTransform));
        print("");
        print("ideal of exceptional divisor:"+string(exceptionalDivisor));
        print("");
        print("ideal of blowup map:"+string(blowUpMap));
}

proc print_tree_node(tree_node node)
{
	print(node.params);
	print(node.blowup);
}


// let z be the hypersurface_variable, and let e denote the degree of f as
// a polynomial in z.
//
// this helper function returns the polynomial by taking 
// f and removing all of its all monomial terms with degree greater 
// than d (as a z- polynomial) :
//
// f = z^d + c_(d-1)z^(d-1) + ... + c_1 z + c_0
//
// and then, if c_(d-1) is non-zero, make the substitution z = z - (c_(d-1) / d)
// to remove the c_(d-1) term.
// 
// this could be inefficient, as we process the terms of f linearly.
static proc format_polynomial(poly f, poly hypersurface_variable, int d)
{
	dbprint("format_polynomial | f: " + string(f) + " hypersurface_of_max_contact:" + string(hypersurface_variable) + " d:" + string(d));

	poly z = hypersurface_variable; //shorten the name a little

	matrix m = coef(f, z);

	poly g = 0;

	int num_terms = size(m) div 2;

	int has_d_min_1_term = 0;
	poly c_d_min_1 = 0;

	for(int i = 1; i <= num_terms; i++)
	{
		int term_deg = deg(m[1,i]);
		poly coefficient = m[2,i];

		if(term_deg > d)
		{
			i++;
			continue;
		}
		

		if(term_deg == d)
		{
			if(coefficient != poly(1))
			{
				dbprint("format_polynomial | warning: coefficient of " + string(hypersurface_variable) + "^" + string(d) + " is not 1");
			}

		}

		else
		{
			if(term_deg == d-1)
			{
				has_d_min_1_term = 1;
				c_d_min_1 = coefficient;
			}

		}

		g = g + coefficient * m[1,i];
	}

	if(has_d_min_1_term)
	{
		// this change of variables gets rid of the c_(d-1) term
		poly s = z - c_d_min_1 / d;
		dbprint("format_polynomial | substituting " + string(z) + " with " + string(s));
		g = subst(g, z, s);
	}

	//divide by leading coefficient if it is a unit
	g = simplify(g, 1);

	dbprint("format_polynomial | result is :" + string(g));

	return(g);
}

//helper function for process_coeff_ideal
static proc process_coeff_ideal_principal(coeff_ideal J, poly hypersurface_variable)
{
	poly generator = J.I[1]; // we assume J has exactly one non-zero generator

	poly f = format_polynomial(generator, hypersurface_variable, J.d);

	matrix m = coef(f, hypersurface_variable);
	
	dbprint("process_coeff_ideal_principal | " + string(f) + " as a polynomial in " + string(hypersurface_variable) + ":");
	dbprint(m);

	// now, for each coefficient, we sum all coefficient ideals.
	coeff_ideal out = ideal(0), 1;

	int num_terms = size(m) div 2;

	for(int i = 1; i <= num_terms; i++)
	{
		int term_deg = deg(m[1,i]);
		if (term_deg == J.d)
		{
			//need the i++ before continue in Singular, not like C.
			i++;
			continue;
		}

		coeff_ideal K;

		K.I = ideal(m[2,i]);
		K.d = J.d - deg(m[1,i]);

		string term_str = "(" + string(m[2,i]) + ")" + string(hypersurface_variable) + "^" + string(term_deg);
		dbprint("process_coeff_ideal_principal | term: " + term_str + " ideal:" + string(K));

		out = out + K;

	}

	return(out);
}

/*
This processes a certain kind of coefficient ideals for the algorithm described for computing the centre of blowup.

requires:
	-hypersurface_variable is just a coordinate variable, say z.
	-Label the ideal J.I as (f_1,...,f_m). Then z = 0 is the hypersurface of max contact of J.I
	-The generator f can be expressed as z^d + (c_(d-2))z^(d-2) + ... + c_1 z + c_0, where the c_i's are polynomials in the remaining ring variables

returns:
	0 if failed. Otherwise, the coefficient ideal required for the algorithm that computes the centre of blowup.
	
*/ 
proc process_coeff_ideal(coeff_ideal J, poly hypersurface_variable)
{
	coeff_ideal out = ideal(0), 1;

	for(int i = 1; i <= size(J.I); i++)
	{
		coeff_ideal K = J.I[i], J.d;
		dbprint("process_coeff_ideal | processing ideal: " + string(K));
		coeff_ideal processed = process_coeff_ideal_principal(K, hypersurface_variable);
		dbprint("process_coeff_ideal | result is :" + string(processed));
		out = out + processed;
	}

	dbprint("process_coeff_ideal | final result is :" + string(out));
	return(out);
}
example
{
	ring R = 0,(x,y,z),dp;
	// coeff_ideal J = ideal(z3-x2yz-x4), 3;
	coeff_ideal J;
	J.I = ideal(z3-x2yz-x4);
	J.d = 3;
	process_coeff_ideal(J, z); // ((-x6y3,x8), 6)
}

//assume I is non-zero
//computes the invariant of I at year 0
//
//TODO: i can make this cleaner by doing all the work in a simple loop
proc compute_invariant_0(ideal I)
{
	if(size(I) == 0)
	{
		dbprint("compute_invariant_0 | input:"+string(I)+" has size 0");
		return(make_null_inv_data());
	}

	list generators = I[1..size(I)]; //the list with f_i, where I = (f_1,...,f_m)
	poly smallest_deg = smallest_total_degree_polynomial(generators);

	poly hypersurface_var = hypersurface_max_contact(smallest_deg);

	if(!rvar(hypersurface_var))
	{
		dbprint("compute_invariant_0 | could not find a hypersurface of max contact for " + string(smallest_deg));
		return(make_null_inv_data());
	}

	// order of vanishing of f_1,...,f_m at the origin corresponds to the f_i with smallest minimum degree
	int d = mindeg(smallest_deg);

	invData data;
	data = update_invariant(data, hypersurface_var, list(d,0));

	coeff_ideal J = I, d;

	// terminate when J has the zero ideal
	while(size(J.I) > 0)
	{
		dbprint("compute_invariant_0 | ideal:" + string(J) + " hypersurface of max contact:" + string(hypersurface_var) + " invariant data:" + string(data));

		coeff_ideal res = process_coeff_ideal(J, hypersurface_var);


		I = res.I;
		d = res.d;

		if(size(I) == 0)
		{
			break;
		}

		generators = I[1..size(I)];

		smallest_deg = smallest_total_degree_polynomial(generators);
		int e = mindeg(smallest_deg);

		//update the hypersurface of max contact
		hypersurface_var = hypersurface_max_contact(smallest_deg);

		if(!rvar(hypersurface_var))
		{
			dbprint("compute_invariant_0 | could not find a hypersurface of max contact for " + string(smallest_deg));
			return(make_null_inv_data());
		}

		res.d = e;
		//update the coefficient ideal
		J = res;

		//update the invariant data
		data = update_invariant(data, hypersurface_var, list(number(e)/number(d), 0));
	}

	// update the invariant with -1 denoting infinity
	data = update_invariant(data, 0, list(-1));
	return(data);
}
example
{
	ideal I = z3 - x2yz - x4;
	invData data = compute_invariant_0(I);
	string(l);
}

static proc dfs_resolve_core(ideal K, int depth, int max_depth, list tree_path)
{

	dbprint("dfs_resolve_core | K:"+string(K));

	// first, update the farthest path
	if(size(tree_path) > size(farthest_path))
	{
		farthest_path = tree_path;
	}

	if(depth >= max_depth)
	{
		dbprint("max depth reached. terminating..");
		return();
	}


	if(size(K) == 0)
	{
		dbprint("received zero ideal. terminating..");
		return();
	}
	else
	{
		list generators = K[1..size(K)];
		if(smallest_total_degree(generators) < 1)
		{
			dbprint("One of the generators have min degree less than 1, terminating..");
			return();
		}
	}

	invData data = compute_invariant_0(K);

	if(is_null(data)) 
	{
		dbprint(" unable to compute invariant for " + string(K) + ". terminating...");
		return();
	}

	list hypersurfaces = data.hypersurfaces;
	ideal centre_of_blowup = hypersurfaces[1..size(hypersurfaces)]; 

	// Compare the current invariant to the parent node
	if(size(tree_path) >= 1)
	{
		tree_node previous_node = tree_path[size(tree_path)];
		invData previous_invariant = previous_node.params.invariant;

		if(!is_null(previous_invariant) && !inv_datacmp_lt(data, previous_invariant))
		{
			dbprint("current invariant is not less than previous");
			dbprint("terminating..");
			return();
		}
	}

	//Record non blowup-related information about this node.
	nodeData params = string(K), depth, data;

	list blow = blowUp(K, centre_of_blowup);

	for(int i = 1; i <= size(blow); i++)
	{
		def Q = blow[i];
		setring(Q);

		blowUpData blowup = varstr(Q), string(aS), string(sT), string(eD), string(bM);
		tree_node node = params,blowup;

		sT = simplify(sT, 1+2+4);
		// proceed with the strict transform
		dfs_resolve_core(sT, depth + 1, max_depth, tree_path + list(node));
	}	
}

proc dfs_resolve(ideal K, int max_depth)
{
	list farthest_path = list();
	export farthest_path;

	invData null_data = list(), list();

	dfs_resolve_core(K, 0, max_depth, list());

	for(int i = 1; i <= size(farthest_path); i++)
	{
		print("///////////////////NODE "+string(i)+"//////////////////");
		print(farthest_path[i]);
	}
}
